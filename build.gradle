plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
    id 'org.jetbrains.kotlin.jvm'
	id 'jacoco'
	id 'info.solidsoft.pitest' version '1.15.0'
}

group = 'com.nano'
version = '0.0.1-SNAPSHOT'

java {
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.liquibase:liquibase-core'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.modelmapper:modelmapper:3.2.0'
	implementation 'commons-io:commons-io:2.15.1'
	implementation 'net.datafaker:datafaker:2.0.2'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mockito:mockito-inline:5.2.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		html.destination file('build/jacocoHtml')
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					'**/com/nano/practicespringboot/PracticeSpringbootApplication*',
					'**/com/nano/practicespringboot/presenters/*',
					'**/com/nano/practicespringboot/enums/*',
					'**/com/nano/practicespringboot/entities/*'
			])
		}))
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			excludes = ['PracticeSpringbootApplication*']
			limit {
				minimum = 0.90
			}
			excludes = [
					'com.nano.practicespringboot.PracticeSpringbootApplication',
					'com.nano.practicespringboot.presenters.**',
					'com.nano.practicespringboot.enums.**',
					'com.nano.practicespringboot.entities.**'
			]
		}
	}
}

pitest {
	targetClasses = ['com.nano.practicespringboot.*']
	pitestVersion = '1.15.0'
	threads = 2
	outputFormats = ['XML', 'HTML']
	timestampedReports = false
	junit5PluginVersion = '1.2.1'
	mutationThreshold = 90
	excludedClasses = [
			'com.nano.practicespringboot.PracticeSpringbootApplication',
			'com.nano.practicespringboot.presenters.**',
			'com.nano.practicespringboot.enums.**',
			'com.nano.practicespringboot.entities.**'
	]
}